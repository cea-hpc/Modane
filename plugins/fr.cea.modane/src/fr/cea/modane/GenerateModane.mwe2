/*******************************************************************************
 * Copyright (c) 2022 CEA
 * This program and the accompanying materials are made available under the
 * terms of the Eclipse Public License 2.0 which is available at
 * http://www.eclipse.org/legal/epl-2.0.
 *
 * SPDX-License-Identifier: EPL-2.0
 * Contributors: see AUTHORS file
 *******************************************************************************/
module fr.cea.modane.GenerateModane

import org.eclipse.xtext.xtext.generator.*
import org.eclipse.xtext.xtext.generator.model.project.*

var rootPath = "../.."
var pluginsPath = "${rootPath}/plugins"
var testsPath = "${rootPath}/tests"

Workflow {
	component = xtext.ModaneXtextGenerator {
		configuration = {
			project = StandardProjectConfig {
				baseName = "fr.cea.modane"
				rootPath = pluginsPath
				runtimeTest = {
					enabled = false
					root = "${testsPath}/fr.cea.modane.tests"
				}
				eclipsePlugin = {
					enabled = true
				}
				genericIde = {
					enabled = true
				}
				eclipsePluginTest = {
					enabled = false
				}
				createEclipseMetaData = true
				
			}
			code = {
				encoding = "UTF-8"
				lineDelimiter = "\n"
				fileHeader = "/*\n * generated by Xtext \${version}\n */"
			}
		}
		language = StandardLanguage {
			name = "fr.cea.modane.Modane"
			fileExtensions = "modane"

			serializer = {
				generateStub = false
			}

			emfGenerator = ecore.EMFGeneratorFragment2 auto-inject {
				generateEdit = true
				editPluginID = "fr.cea.modane.edit"
				editDirectory = "fr.cea.modane.edit/src-gen"
				generateEditor = true
				editorPluginID = "fr.cea.modane.editor"
				editorDirectory = "fr.cea.modane.editor/src-gen"
			}
			
			formatter = {
				generateStub = true
				generateXtendStub = true
			}
			
		}
	}

	// To be able to generate edit an editor source code
	bean = org.eclipse.emf.mwe.utils.StandaloneSetup {
		platformUri = ".."
	}
}

